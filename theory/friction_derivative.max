/* ----------------------------------------------------------
Maxima File to compute total derivative expressions for scrf

@author: Nick Gibbons
-----------------------------------------------------------*/

mass:rho*v*A - rho1*v1*A1;
mom:rho*v*v*A + p*A - rho1*v1*v1*A1 - p1*A1 -(p+p1)/2*dA + c*f*rho1*v1*v1;
nrg:rho*v*A*(u+v*v/2) + p*A*v - rho1*v1*A1*(u1+v1*v1/2) - p1*A1*v1;
eos:p-rho*R*u/cv;

dRdU2:matrix([diff(mass,rho), diff(mass,v), diff(mass,p), diff(mass,u)],
             [diff(mom, rho), diff(mom, v), diff(mom, p), diff(mom, u)],
             [diff(nrg, rho), diff(nrg, v), diff(nrg, p), diff(nrg, u)],
             [diff(eos, rho), diff(eos, v), diff(eos, p), diff(eos, u)]);

dRdU1:matrix([diff(mass,rho1), diff(mass,v1), diff(mass,p1), diff(mass,u1)],
             [diff(mom, rho1), diff(mom, v1), diff(mom, p1), diff(mom, u1)],
             [diff(nrg, rho1), diff(nrg, v1), diff(nrg, p1), diff(nrg, u1)],
             [diff(eos, rho1), diff(eos, v1), diff(eos, p1), diff(eos, u1)]);

dRdf:matrix([diff(mass, f)], [diff(mom,f)], [diff(nrg, f)], [diff(eos, f)]);
dU1df:matrix([dr1df], [dv1df], [dp1df], [du1df]);

display2d:false$
rhs_a:-dRdU1.dU1df - dRdf;

rhs:matrix([rhs0], [rhs1], [rhs2], [rhs3]);


Jinv:invert(dRdU2)$

dUdf:Jinv.rhs$


drdf:dUdf[1,1]$
drdf:fullratsimp(drdf);
print("")$

dvdf:dUdf[2,1]$
dvdf:fullratsimp(dvdf);
print("")$

dpdf:dUdf[3,1]$
dpdf:fullratsimp(dpdf);
print("")$

dudf:dUdf[4,1]$
dudf:fullratsimp(dudf);
print("")$
